###########################################################
#
# Convert from format MM/DD/YYYY 'American format' to DD/MM/YYYY 'UK format'
# e.g. 12/25/2023 becomes 25/12/2023 
#
###########################################################

rule "US DATE to UK DATE"
when
exist "//*[local-name()='date'][1]"
then

#Day value into TEMP1
set TEMP"1" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"1","^.*?/")
remove substring using regex (TEMP"1","(/.*)")

#Month value into TEMP2
set TEMP"2" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"2","(/.*)")

#Year value into TEMP3
set TEMP"3" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"3","^(.*)/")

#Combine values to form DD/MM/YYYY
concatenate with delimiter (TEMP"1",TEMP"2","/")
concatenate with delimiter (TEMP"1",TEMP"3","/")

set TEMP"1" in "dc"."date"

end


###########################################################
#
# Convert from MM/DD/YYYY 'American format' to UK written format (Ordinal-Day Month Year)
# e.g. 12/25/2023 --> 25th December 2023
#
###########################################################

rule "US DATE to UK DATE in Written Format"
when
exist "//*[local-name()='date'][1]"
then

#Day value into TEMP1
set TEMP"1" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"1","^.*?/")
remove substring using regex (TEMP"1","(/.*)")

add suffix (TEMP"1","th")
remove substring using regex (TEMP"1","^0")
add prefix (TEMP"1","/")

replace string by string (TEMP"1","/1th","1st")
replace string by string (TEMP"1","/2th","2nd")
replace string by string (TEMP"1","/3th","3rd")
remove substring using regex (TEMP"1","^/")


#Month value into TEMP2
set TEMP"2" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"2","(/.*)")

replace string by string (TEMP"2","12","December")
replace string by string (TEMP"2","11","November")
replace string by string (TEMP"2","10","October")
replace string by string (TEMP"2","09","September")
replace string by string (TEMP"2","08","August")
replace string by string (TEMP"2","07","July")
replace string by string (TEMP"2","06","June")
replace string by string (TEMP"2","05","May")
replace string by string (TEMP"2","04","April")
replace string by string (TEMP"2","03","March")
replace string by string (TEMP"2","02","February")
replace string by string (TEMP"2","01","January")

#Year value into TEMP3
set TEMP"3" to xpath "//*[local-name()='date'][1]"
remove substring using regex (TEMP"3","^(.*)/")

#Combine
concatenate with delimiter (TEMP"1",TEMP"2"," ")
concatenate with delimiter (TEMP"1",TEMP"3"," ")

remove leading and trailing spaces (TEMP"1")
set TEMP"1" in "dc"."date"

end
